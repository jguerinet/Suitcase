/*
 * Copyright 2016-2018 Julien Guerinet
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.github.ben-manes.versions'

buildscript {

    // Versions shared across modules
    ext.android_support_version = '27.1.0'
    ext.espresso_version = '2.2.2'
    ext.junit_version = '4.12'
    ext.kotlin_version = '1.2.31'
    ext.threetenabp_version = '1.0.5'

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.16"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

subprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'org.jetbrains.dokka-android'
    apply plugin: 'com.github.dcendents.android-maven'

    android {
        compileSdkVersion Integer.valueOf(suitcase_target_sdk)

        defaultConfig {
            minSdkVersion suitcase_min_sdk
            targetSdkVersion suitcase_target_sdk
            versionName suitcase_version

            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
        androidTestImplementation("com.android.support.test.espresso:espresso-core:$espresso_version", {
            exclude group: 'com.android.support', module: 'support-annotations'
        })
        testImplementation "junit:junit:$junit_version"
    }

    apply from: "https://raw.githubusercontent.com/jguerinet/Gradle-Artifact-Scripts/master/kotlin-artifacts.gradle"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
